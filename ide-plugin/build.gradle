buildscript {
    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.4.2"
}

dependencies {
    def withoutKotlinAndMySql = {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'mysql'
    }
    def withoutSlfAndKotlinAndMySql = {
        exclude group: 'org.slf4j'//, module: 'slf4j-api'
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'mysql'
    }

    compile project(':ruby-call-signature'), withoutKotlinAndMySql
    compile project(':storage-server-api'), withoutSlfAndKotlinAndMySql
    compile project(':contract-creator'), withoutSlfAndKotlinAndMySql
    compile project(':state-tracker'), withoutSlfAndKotlinAndMySql

    // https://mvnrepository.com/artifact/com.h2database/h2
    compile group: 'com.h2database', name: 'h2', version: '1.4.196'

}

sourceSets {
    main.java.srcDirs = ['src']
    main.resources.srcDirs = ['resources']
    test.java.srcDirs = ['src/test/java']
    test.resources.srcDirs=['src/test/testData']
}

intellij {
    version ideaVersion
    pluginName 'ruby-runtime-stats'
    plugins = ["yaml", "org.jetbrains.plugins.ruby:$rubyPluginVersion"]
}

patchPluginXml {
    sinceBuild '191.5109.14'
    untilBuild '191.*'
    version '0.1.1'
}

prepareSandbox.doLast {
    def destDir = "$it.destinationDir/$intellij.pluginName"
    copy {
        from sourceSets.main.resources.include("**/*.rb", "**/*.db")
        into destDir
    }
}

test {
    testLogging.showStandardStreams = true
}

